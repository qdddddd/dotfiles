#!/bin/zsh

setopt EXTENDED_GLOB
setopt +o nomatch

git submodule update --init --recursive

# Create local bin sym links

mkdir -p $HOME/.local/bin
BINDIR=$HOME/.local/bin

for xfile in ${PWD}/bin/*; do
    ln -s "$xfile" "$BINDIR/${xfile:t}"
done

#############################

# Some useful functions

is_mac() {
    [[ "$OSTYPE" == "darwin"* ]]
}

is_linux() {
    [[ "$OSTYPE" == 'linux-gnu'* ]]
}

command_exists() {
    (( $+commands[$1] ));
}

standard_install() {
    if ! (command_exists $1); then
        if is_mac; then
            brew install $1
        elif is_linux; then
            sudo apt-get install $1
        fi
    fi
}

#############################

# Intall homebrew
if is_mac && ! (command_exists brew); then
    /bin/bash -c "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install.sh)"
fi

# Install pip
if ! (command_exists pip3); then
    if is_linux; then
        sudo apt-get install python3 python3-distutils python3-apt
    fi
    curl https://bootstrap.pypa.io/get-pip.py -o get-pip.py
    sudo python3 get-pip.py
    rm get-pip.py
fi

# Install ag
if ! (command_exists ag); then
    if is_linux; then
        sudo apt-get install silversearcher-ag
    elif is_mac; then
        brew install the_silver_searcher
    fi
fi

# Install fzf
if [ ! -s /usr/local/opt/fzf/ ]; then
    sudo git clone --depth 1 https://github.com/junegunn/fzf.git /usr/local/opt/fzf
    sudo /usr/local/opt/fzf/install
fi

# Install autojump
standard_install autojump

# Install thefuck
if ! (command_exists fuck); then
    sudo pip3 install thefuck
fi

# Install vim-plug
if [ ! -s $HOME/.vim/autoload/plug.vim ]; then
    curl -fLo ~/.vim/autoload/plug.vim --create-dirs \
        https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
fi

# Install dependencies for Coc
if ! (command_exists node); then
    curl -sL install-node.now.sh | sudo bash
fi

if ! (command_exists yarn); then
    curl --compressed -o- -L https://yarnpkg.com/install.sh | bash
fi

# Create zsh config sym links
for rcfile in "${PWD}"/zsh/configs/*; do
    ln -s "$rcfile" "$HOME/.${rcfile:t}"
done
source $HOME/.zshrc

# Create .zshrc.local
touch $HOME/.zshrc.local
cat <<EOF >$HOME/.zshrc.local
prompt_bernoulli_setup 66
EOF

# Create vim config sym links
DOT_VIM_DIR=${PWD}/vim
#for lcfile in ${DOT_VIMRC_DIR}/^*(README.md|nvim)*; do
for lcfile in ${DOT_VIM_DIR}/(rc|ext)/*; do
    ln -s "$lcfile" "$HOME/.vim/${lcfile:t}"
done

# Create nvim config sym links
if (command_exists nvim); then
    NVIM_CONFIG_DIR=$HOME/.config/nvim
    mkdir -p ${NVIM_CONFIG_DIR}
    for lcfile in ${DOT_VIM_DIR}/(nvim|ext)/*; do
        ln -s "$lcfile" "${NVIM_CONFIG_DIR}/${lcfile:t}"
    done

    nvim +UpdateRemotePlugins +qa!
fi

vi +PlugInstall +PlugClean +qa!
rm -f $HOME/.fzf.zsh $HOME/.fzf.bash

# Install tmux
standard_install tmux
git clone https://github.com/tmux-plugins/tpm $HOME/.tmux/plugins/tpm

# Install tree
standard_install tree

# Install Alarcritty
if is_mac; then
    if ! (command_exists alacritty); then
        brew install alacritty

        # Enable font smoothing
        defaults write -g CGFontRenderingFontSmoothingDisabled -bool NO
        defaults -currentHost write -globalDomain AppleFontSmoothing -int 2
    fi
fi

#############################

# Create sym links for dot configs
for dotfile in ${PWD}/dotconf/*; do
    ln -s "$dotfile" "$HOME/.${dotfile:t}"
done

if ! is_mac; then
    rm $HOME/.alacritty.yml
fi
