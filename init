#!/bin/zsh

setopt EXTENDED_GLOB
setopt +o nomatch

git submodule update --init --recursive

# Create local bin sym links
mkdir -p $HOME/.local/bin
BINDIR=$HOME/.local/bin

for xfile in ${PWD}/bin/*; do
    ln -s "$xfile" "$BINDIR/${xfile:t}"
done

# Install pip
if ! (( $+commands[pip3] )); then
    if [[ "$OSTYPE" == 'linux-gnu'* ]]; then
        sudo apt-get install python3 python3-distutils python3-apt
    fi
    curl https://bootstrap.pypa.io/get-pip.py -o get-pip.py
    sudo python3 get-pip.py
    rm get-pip.py
fi

# Install ag
if ! (( $+commands[ag] )); then
    if [[ "$OSTYPE" == 'linux-gnu'* ]]; then
        sudo apt-get install silversearcher-ag
    elif [[ "$OSTYPE" == "darwin"* ]]; then
        # Install homebrew if not exists
        if ! (( $+commands[brew] )); then
            /bin/bash -c \
                "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install.sh)"
        fi

        brew install the_silver_searcher
    fi
fi

# Install fzf
if [ ! -s /usr/local/opt/fzf/ ]; then
    sudo git clone --depth 1 https://github.com/junegunn/fzf.git /usr/local/opt/fzf
    sudo /usr/local/opt/fzf/install
fi

# Install autojump
if ! (( $+commands[autojump] )); then
    if [[ "$OSTYPE" == 'linux-gnu'* ]]; then
        sudo apt-get install autojump
    elif [[ "$OSTYPE" == "darwin"* ]]; then
        brew install autojump
    fi
fi

# Install thefuck
if ! (( $+commands[fuck] )); then
    sudo pip3 install thefuck
fi

# Install vim-plug
if [ ! -s $HOME/.vim/autoload/plug.vim ]; then
    curl -fLo ~/.vim/autoload/plug.vim --create-dirs \
        https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
fi

# Install dependencies for Coc
if ! (( $+commands[node] )); then
    curl -sL install-node.now.sh | sudo bash
fi

if ! (( $+commands[yarn] )); then
    curl --compressed -o- -L https://yarnpkg.com/install.sh | bash
fi

# Create vim config sym links
for lcfile in ${PWD}/vim/configs/^*(README.md|init)*; do
    ln -s "$lcfile" "$HOME/.vim/${lcfile:t}"
done
mkdir $HOME/.vim/tempfiles
vim +PlugInstall +PlugClean +qa!
rm -f $HOME/.fzf.zsh $HOME/.fzf.bash

# Create zsh config sym links
for rcfile in "${PWD}"/zsh/configs/*; do
    ln -s "$rcfile" "$HOME/.${rcfile:t}"
done
source $HOME/.zshrc

# Create tmux.conf sym link
ln -s "${PWD}/tmux.conf" "$HOME/.tmux.conf"
git clone https://github.com/tmux-plugins/tpm $HOME/.tmux/plugins/tpm

# Create git global config sym links
ln -s "${PWD}/gitconfig" "$HOME/.gitconfig"
ln -s "${PWD}/gitignore_global" "$HOME/.gitignore_global"
