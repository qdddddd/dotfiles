# Load dependencies.
pmodload 'helper'

function prompt_bernoulli_precmd {
    setopt LOCAL_OPTIONS
    prompt_opts=(cr percent sp subst)
    unsetopt XTRACE KSH_ARRAYS

    LEFT_CONTENT=${USER}'@'${HOST}' » %~ '
    LEFT='%F{$COLOR}%B'${LEFT_CONTENT}
    local pwdsize=${#${(%):-%~}}

    # Get Git repository information.
    if (( $+functions[git-info] )); then
        git-info
    fi
    RIGHT=${git_info:+${(e)git_info[prompt]}}

    local middle_width=$((COLUMNS-${#LEFT_CONTENT}+3-${pwdsize}-${#RIGHT}))


    if ((${#RIGHT} == 0)); then
        MIDDLE=''
    else
        MIDDLE=${(r:$middle_width:: :)}
    fi

    if ((middle_width < 0)); then
        RIGHT='%f'
    fi

    #echo $((${#LEFT_CONTENT}-3+${pwdsize})) ${#MIDDLE} ${#RIGHT} $COLUMNS
}

function prompt_bernoulli_help {
    cat <<EOT
This prompt's color is customizable:

prompt bernoulli [<color>]

In ~/.zpreztorc:
zstyle ':prezto:module:prompt' theme 'bernoulli' ['<color>']

If these options are not provided, the color defaults to cyan.
EOT
}

function prompt_bernoulli_preview {
    if (( $# > 0 )); then
        prompt_preview_theme 'bernoulli' "$@"
    else
        prompt_preview_theme 'bernoulli' "cyan"
    fi
}

function prompt_bernoulli_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent sp subst)

    # Assign colors.
    if [[ -n "$1" ]]; then
        COLOR="$1"
    else
        COLOR='cyan'
    fi

    # Load required functions.
    autoload -Uz add-zsh-hook

    # Add hook for calling git-info before each command.
    add-zsh-hook precmd prompt_bernoulli_precmd

    # Set git-info parameters.
    # Must use Powerline font, for \uE0A0 to render.
    zstyle ':prezto:module:git:info' verbose 'yes'
    zstyle ':prezto:module:git:info:dirty' format "✱"
    #zstyle ':prezto:module:git:info:clean' format ""
    zstyle ':prezto:module:git:info:ahead' format "ﰵ"
    zstyle ':prezto:module:git:info:behind' format "ﰬ"
    zstyle ':prezto:module:git:info:branch' format " %%B %b%%b"
    #zstyle ':prezto:module:git:info:added' format '☁︎'
    zstyle ':prezto:module:git:info:keys' format \
        'prompt' '%b%C%D%A%B' \
        'rprompt' ''

    # Define prompts.
    PROMPT=$'${LEFT} ${MIDDLE} ${RIGHT}'
    PROMPT+=$'\n→ %f%b '
}
prompt_bernoulli_setup "$@"

#PROMPT='%F{$COLOR}%n@%m » %1~ %f → '
